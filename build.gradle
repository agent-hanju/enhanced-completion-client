plugins {
  id 'java-library'
  id 'maven-publish'
}

group = 'me.hanju'
version = '0.2.0'
description = 'Enhanced OpenAI-compatible Chat Completion client with streaming and citation support'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
  withSourcesJar()
  withJavadocJar()
}

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {
  api 'com.github.agent-hanju:fluxhandle:0.4.2'

  implementation 'com.github.agent-hanju:content-stream-adapter:0.1.6'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.2'

  compileOnly 'org.springframework:spring-webflux:7.0.3'
  compileOnly 'org.slf4j:slf4j-api:2.0.17'

  compileOnly 'org.projectlombok:lombok:1.18.42'
  annotationProcessor 'org.projectlombok:lombok:1.18.42'

  // test
  testImplementation 'org.junit.jupiter:junit-jupiter:6.0.2'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.2'
  testImplementation 'org.assertj:assertj-core:3.27.6'

  testImplementation 'org.springframework:spring-webflux:7.0.3'
  testImplementation 'org.springframework:spring-context:7.0.3'
  testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.17'
  testImplementation 'io.projectreactor.netty:reactor-netty-http:1.3.2'

  testCompileOnly 'org.projectlombok:lombok:1.18.42'
  testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

}
tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
  options.encoding = 'UTF-8'
  options.charSet = 'UTF-8'
  options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named('test') {
  useJUnitPlatform()
  systemProperty 'file.encoding', 'UTF-8'
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      pom {
        name = "Enhanced Completion Client"
        description = project.description
        url = 'https://github.com/agent-hanju/enhanced-completion-client'
        licenses {
          license {
            name = 'MIT License'
            url = 'https://opensource.org/licenses/MIT'
          }
        }
        developers {
          developer {
            id = 'agent-hanju'
            name = 'hanju'
            email = 'agent.hanju@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/agent-hanju/enhanced-completion-client.git'
          developerConnection = 'scm:git:ssh://github.com/agent-hanju/enhanced-completion-client.git'
          url = 'https://github.com/agent-hanju/enhanced-completion-client'
        }
        issueManagement {
          system = 'GitHub Issues'
          url = 'https://github.com/agent-hanju/enhanced-completion-client/issues'
        }
      }
    }
  }
}
